; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Prismatik (unofficial) 64bit"
#define MyAppVersion GetFileVersion("content/Prismatik.exe")
#define MyAppPublisher "Patrick Siegler"
#define MyAppURL "https://github.com/psieg/Lightpack"
#define MyAppExeName "Prismatik.exe"
#define UserSettingsDirName "{%USERPROFILE|{app}}\Prismatik"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppID={{ABD88CE7-1FFA-416C-96CA-CCC6F2B34236}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf64}\Prismatik
DefaultGroupName=Prismatik
AllowNoIcons=true
LicenseFile=gpl-3.0.txt
OutputBaseFilename={#MyAppName} Setup {#MyAppVersion}
SetupIconFile=Prismatik.ico
Compression=lzma/Max
SolidCompression=true
AppCopyright=
UninstallDisplayName={#MyAppName} {cm:RemoveOnly}
UninstallDisplayIcon={app}\Prismatik.exe
VersionInfoDescription=Lightpack Software
AppMutex=LightpackAppMutex
VersionInfoVersion={#MyAppVersion}
VersionInfoProductName=Prismatik
VersionInfoProductVersion={#MyAppVersion}
Uninstallable=true

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1
Name: "startupicon"; Description: "{cm:CreateStartupIcon}"; GroupDescription: "{cm:StartupGroup}"; Flags: unchecked
Name: "adminstartuptask"; Description: "{cm:CreateAdminStartupTask}"; GroupDescription: "{cm:StartupGroup}"; Flags: unchecked

[Files]
Source: "content/Prismatik.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "Prismatik.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "content/Qt6Core.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "content/Qt6Gui.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "content/Qt6Network.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "content/Qt6SerialPort.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "content/Qt6Widgets.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "gpl-3.0.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "content/libraryinjector.dll"; DestDir: "{app}"; MinVersion: 6.1.7600; Flags: 32bit regserver ignoreversion
Source: "content/offsetfinder.exe"; DestDir: "{app}"; MinVersion: 6.1.7600; Flags: 32bit ignoreversion
Source: "content/prismatik-hooks.dll"; DestDir: "{app}"; MinVersion: 6.1.7600; Flags: ignoreversion
Source: "content/prismatik-unhook.dll"; DestDir: "{app}"; MinVersion: 6.1.7600; Flags: ignoreversion
Source: "content/prismatik-hooks32.dll"; DestDir: "{app}"; MinVersion: 6.1.7600; Flags: 32bit ignoreversion
Source: "content/prismatik-unhook32.dll"; DestDir: "{app}"; MinVersion: 6.1.7600; Flags: 32bit ignoreversion
Source: "content/UpdateElevate.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "content/platforms/*"; DestDir: "{app}\platforms"; Flags: ignoreversion
Source: "content/styles/*"; DestDir: "{app}\styles"; Flags: ignoreversion
Source: "content/tls/qopensslbackend.dll"; DestDir: "{app}\tls"; Flags: ignoreversion
Source: "content/Plugins/*"; DestDir: "{#UserSettingsDirName}\Plugins"; Flags: onlyifdoesntexist createallsubdirs recursesubdirs
; Use ssleay32.dll and libeay32.dll here for OpenSSL < 1.1, 1_1 instead of 3 for OpenSSL 1.1
Source: "content/libcrypto-3-x64.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "content/libssl-3-x64.dll"; DestDir: "{app}"; Flags: ignoreversion
; These are needed only when including the bass library for sound visualization
Source: "content/bass.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "content/basswasapi.dll"; DestDir: "{app}"; Flags: ignoreversion
; These files are needed only when building with MSVC - otherwise just comment them
Source: "content/msvcr*.dll"; DestDir: "{app}"; Flags: ignoreversion skipifsourcedoesntexist
Source: "content/msvcp*.dll"; DestDir: "{app}"; Flags: ignoreversion skipifsourcedoesntexist
Source: "content/vcruntime*.dll"; DestDir: "{app}"; Flags: ignoreversion skipifsourcedoesntexist
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"; Flags: excludefromshowinnewinstall
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"; Flags: excludefromshowinnewinstall
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon
Name: "{commonstartup}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: startupicon

[Run]
Filename: "{app}\UpdateElevate"; Parameters: "install"; Flags: runhidden
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
Filename: "https://github.com/psieg/Lightpack/wiki"; Description: "{cm:OpenWiki}"; Flags: postinstall unchecked shellexec skipifsilent runasoriginaluser
Filename: "schtasks"; Parameters: "/create /f /RU Administrators /RL HIGHEST /SC onlogon /TN ""Prismatik with Admin Rights"" /TR ""{app}\{#MyAppExeName}"""; Tasks: adminstartuptask; Flags: runhidden skipifsilent

[UninstallRun]
Filename: "{app}\UpdateElevate"; Parameters: "uninstall"; Flags: runhidden
Filename: "schtasks"; Parameters: "/delete /f /TN ""Prismatik with Admin Rights"""; Flags: runhidden

[UninstallDelete]
Name: "{app}\*.*"; Type: filesandordirs
Name: (app); Type: dirifempty

[CustomMessages]
; Startup group name
russian.StartupGroup =автоматический запуск:
english.StartupGroup =Automatic Startup:

; Startup icon name
russian.CreateStartupIcon=Добавить в папку запуска
english.CreateStartupIcon=Add to startup folder

; Admin task name
russian.CreateAdminStartupTask =Автоматический запуск с правами администратора (Не рекомендуется)
english.CreateAdminStartupTask =Automatically launch with admin rights (not recommended)

; Uninstall name
russian.RemoveOnly =(удалить только)
english.RemoveOnly =(remove only)

; Docs link name
russian.OpenWiki =Открытая документация
english.OpenWiki =Open documentation

; Docs link name
russian.IAgree =Я согласен
english.IAgree =I agree

; https://stackoverflow.com/questions/51290580/on-inno-setup-license-page-replace-checkboxes-by-i-agree-button
[Code]

procedure InitializeWizard();
begin
  // Hide radio buttons and pre-select "accept", to enable "next" button
  WizardForm.LicenseAcceptedRadio.Checked := True;
  WizardForm.LicenseAcceptedRadio.Visible := False;
  WizardForm.LicenseNotAcceptedRadio.Visible := False;
  WizardForm.LicenseMemo.Height :=
    WizardForm.LicenseNotAcceptedRadio.Top +
    WizardForm.LicenseNotAcceptedRadio.Height -
    WizardForm.LicenseMemo.Top - ScaleY(5);
end;

procedure CurPageChanged(CurPageID: Integer);
begin
  // Dubious, but you have asked for it
  if CurPageID = wpLicense then
  begin
    WizardForm.NextButton.Caption := '&I agree';
  end;
end;